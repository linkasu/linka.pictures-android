plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'org.jetbrains.kotlin.android'
}

def envOrProperty = { String key ->
    return System.getenv(key) ?: (project.hasProperty(key) ? project.property(key) : null)
}

File keystoreFile = null
def keystorePathFromEnv = envOrProperty("ANDROID_KEYSTORE_PATH")?.toString()
if (keystorePathFromEnv) {
    keystoreFile = file(keystorePathFromEnv)
} else {
    def defaultFile = rootProject.file("main.jks")
    if (defaultFile.exists()) {
        keystoreFile = defaultFile
    }
}

def keystorePassword = envOrProperty("ANDROID_KEYSTORE_PASSWORD")?.toString()
def keyAliasValue = envOrProperty("ANDROID_KEY_ALIAS")?.toString() ?: "key0"
def keyPasswordValue = envOrProperty("ANDROID_KEY_PASSWORD")?.toString() ?: keystorePassword
def hasReleaseSigningConfig = keystoreFile?.exists() && keystorePassword && keyAliasValue && keyPasswordValue

android {
    namespace 'su.linka.pictures'
    compileSdk 34

    defaultConfig {
        applicationId "su.linka.pictures"
        minSdk 21
        targetSdk 34
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (hasReleaseSigningConfig) {
                storeFile keystoreFile
                storePassword keystorePassword
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasReleaseSigningConfig) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:4.9.2"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation platform('com.google.firebase:firebase-bom:33.5.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    annotationProcessor "com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2"
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'
    implementation 'net.lingala.zip4j:zip4j:2.11.5'
    implementation project(':shared')

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:core-ktx:1.6.1'
}
